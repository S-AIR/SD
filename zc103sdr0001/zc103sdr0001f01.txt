*&---------------------------------------------------------------------*
*& Include          ZC103SDR0001F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form set_init_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_init_value .

  DATA : lv_low_date  TYPE sy-datum,
         lv_high_date TYPE sy-datum.

*-- Search date
  lv_low_date = '20250622'.

  CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL'
    EXPORTING
      date      = sy-datum
      days      = 00
      months    = 03
      signum    = '+'
      years     = 00
    IMPORTING
      calc_date = lv_high_date.

*  lv_high_date = sy-datum.

  so_date = VALUE #( sign    = 'I'
                     option  = 'BT'
                     low     = lv_low_date
                     high    = lv_high_date
                    ).
  APPEND so_date.

*  pa_schid = 'KSA09376'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_schedule_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_schedule_data .

  DATA: lv_date_from TYPE sy-datum,
        lv_date_to   TYPE sy-datum.

  lv_date_from = so_date-low(8).
  lv_date_to = so_date-high(8).

  SELECT scheduleid, departdate, departtime, arrivetime, arrivedate,
         countryfrom, countryto
    FROM zc103sdt0008
*    WHERE departdate IN @so_date
     WHERE departdate BETWEEN @lv_date_from AND @lv_date_to
     AND ( scheduleid EQ @pa_schid   OR @pa_schid IS INITIAL )
    ORDER BY departdate, departtime ASCENDING
    INTO CORRESPONDING FIELDS OF TABLE @gt_schedule.

  IF gt_schedule IS INITIAL.
    MESSAGE s003 DISPLAY LIKE 'E'.
    STOP.
  ELSE.
    MESSAGE s008 WITH sy-dbcnt.
  ENDIF.

*-- Get Domain Value
  SELECT
    FROM dd07v
    FIELDS domvalue_l, ddtext
    WHERE domname = 'ZC103D_FI_LAND'
       OR domname = 'ZC103D_SD_BOOK_STATUS'
    INTO CORRESPONDING FIELDS OF TABLE @gt_dd07v.

  PERFORM set_data.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen .


  IF go_container IS NOT BOUND.

    CLEAR : gt_fcat, gs_fcat, gt_tfcat, gs_tfcat, gt_bfcat, gs_bfcat.

    " Set Field Catalog 1             Key Fieldname  Ref_tab  Just Emphasize
    PERFORM set_field_catalog USING : 'X' 'SCHEDULEID' 'ZC103SDT0008' 'C' ' ',
                                      ' ' 'COUNTRYFROM' 'ZC103SDT0008' 'L' 'X',
                                      ' ' 'COUNTRYTO' 'ZC103SDT0008' 'L' 'X',
                                      ' ' 'DEPARTDATE' 'ZC103SDT0008' 'C' ' ',
                                      ' ' 'DEPARTTIME' 'ZC103SDT0008' 'C' ' ',
                                      ' ' 'ARRIVEDATE' 'ZC103SDT0008' 'C' ' ',
                                      ' ' 'ARRIVETIME' 'ZC103SDT0008' 'C' ' '.

    " 예약번호, 예약자, 예약일자, 예약유형, 예약 수량, 총 결제금액, (결제 상태)
    " Set Field Catalog 2             Key Fieldname  Ref_tab  Just Emphasize
    PERFORM set_field_catalog2 USING : 'X' 'BOOKINGID' 'ZC103SDT0011' 'C' ' ',
                                      ' ' 'BOOKER' ' ' ' ' ' ',
                                      ' ' 'BOOKINGDATE' 'ZC103SDT0011' 'C' ' ',
                                      ' ' 'COUNTRYFROM' ' ' ' ' 'X',
                                      ' ' 'COUNTRYTO' ' ' ' ' 'X',
                                      ' ' 'BOOKTYPE' ' ' 'C' 'X',
                                      ' ' 'TOTALTICKET' 'ZC103SDT0011' ' ' ' ',
                                      ' ' 'TOTALPRICE' 'ZC103SDT0011' ' ' ' ',
                                      ' ' 'CURRENCY' 'ZC103SDT0011' ' ' ' ',
                                      ' ' 'STATUS_TEXT' ' ' ' ' 'X'.

    " 예약번호, 예약일자, 이코노미 수량, 프레스티지수량, 총 수량,
    " 가격조건(적용된 할인), 할인 가격, 마일리지 사용량, 할인/마일리지 적용 후 최종 금액, (결제 상태)
    " Set Field Catalog 3             Key Fieldname  Ref_tab  Just Emphasize
    PERFORM set_field_catalog3 USING : 'X' 'BOOKINGID' 'ZC103SDT0011' 'C' ' ',
                                       ' ' 'CONDITIONID' 'ZC103SDT0003' ' ' ' ' ,
                                       ' ' 'CONDITIONTYPE' 'ZC103SDT0003' ' ' 'X' ,
                                       ' ' 'DISCOUNTRATETEXT' ' ' 'C' 'X' ,
                                       ' ' 'TOTALTICKET' 'ZC103SDT0011' ' ' ' ',
                                       ' ' 'UNIT_PRICE' 'ZC103SDT0007' ' ' ' ',
                                       ' ' 'DISCOUNTAMOUNT' ' ' ' ' ' ' ,
                                       ' ' 'USED_MILEAGE' 'ZC103SDT0007' ' ' ' ',
                                       ' ' 'TAX_AMOUNT' 'ZC103SDT0007' ' ' ' ',
                                       ' ' 'TOTALPRICE' 'ZC103SDT0011' ' ' ' ',
                                       ' ' 'CURRENCY' ' ' ' ' ' '.

    " Set Layout [l: zebra, cwidth, sel_mod, v: report, handle]
    PERFORM set_layout.

    PERFORM create_object.                         " 컨테이너 생성

    " Set Handler FOR alv
    SET HANDLER : lcl_event_handler=>hotspot_click FOR go_grid_sched,
                  lcl_event_handler=>hotspot_click FOR go_grid_detail,
                  lcl_event_handler=>double_click FOR go_grid_booking,
                  lcl_event_handler=>top_of_page FOR go_grid_sched,
                  lcl_event_handler=>toolbar FOR go_grid_booking,
                  lcl_event_handler=>user_command FOR go_grid_booking.

    PERFORM register_event. " 문서 초기화

    gs_variant-handle = 'ALV1'.
    CALL METHOD go_grid_sched->set_table_for_first_display
      EXPORTING
        is_variant      = gs_variant
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_layout1
      CHANGING
        it_outtab       = gt_disp_sch
        it_fieldcatalog = gt_fcat.

    gs_variant-handle = 'ALV2'.
    CALL METHOD go_grid_booking->set_table_for_first_display
      EXPORTING
        is_variant      = gs_variant
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_layout2
      CHANGING
        it_outtab       = gt_disp_book
        it_fieldcatalog = gt_tfcat.

    gs_variant-handle = 'ALV3'.
    CALL METHOD go_grid_detail->set_table_for_first_display
      EXPORTING
        is_variant      = gs_variant
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_layout3
      CHANGING
        it_outtab       = gt_disp_detail
        it_fieldcatalog = gt_bfcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_dyndoc_id
    EXPORTING
      style = 'ALV_GRID'.

*-- Top 문서용 도킹 컨테이너
  CREATE OBJECT go_top_cont
    EXPORTING
      side      = cl_gui_docking_container=>dock_at_top
      extension = 50.

*-- 1. Main Docking container
  CREATE OBJECT go_container
    EXPORTING
      side      = cl_gui_docking_container=>dock_at_left
      extension = 5000.

*-- 2. 화면 좌우 split left(항공기 스케줄), right(예약 정보)
  CREATE OBJECT go_split_main
    EXPORTING
      parent  = go_container
      rows    = 1
      columns = 2.

*-- 2-1. Patch container
  CALL METHOD go_split_main->get_container
    EXPORTING
      row       = 1
      column    = 1
    RECEIVING
      container = go_left_cont.

  CALL METHOD go_split_main->get_container
    EXPORTING
      row       = 1
      column    = 2
    RECEIVING
      container = go_right_cont.

*-- 3. 우측 내부 상하 split : 예약 / 상세
  CREATE OBJECT go_split_right
    EXPORTING
      parent  = go_right_cont
      rows    = 2
      columns = 1.

*-- 3-1. Patch container
  CALL METHOD go_split_right->get_container
    EXPORTING
      row       = 1
      column    = 1
    RECEIVING
      container = go_right_top_cont.

  CALL METHOD go_split_right->get_container
    EXPORTING
      row       = 2
      column    = 1
    RECEIVING
      container = go_right_bot_cont.

  CALL METHOD go_split_right->set_row_height
    EXPORTING
      id     = 1
      height = 70.

*-- 4. Patch ALV
  CREATE OBJECT go_grid_sched
    EXPORTING
      i_parent = go_left_cont.

  CREATE OBJECT go_grid_booking
    EXPORTING
      i_parent = go_right_top_cont.

  CREATE OBJECT go_grid_detail
    EXPORTING
      i_parent = go_right_bot_cont.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

  gs_layout1-zebra      = abap_true.
  gs_layout1-cwidth_opt = 'A'.
  gs_layout1-sel_mode   = 'D'.
  gs_layout1-grid_title = '항공편 스케줄'.

  gs_variant-report    = sy-repid.

  gs_layout2 = CORRESPONDING #( gs_layout1 ).
  gs_layout2-grid_title = '예약 내역'.

  gs_layout3 = CORRESPONDING #( gs_layout1 ).
  gs_layout3-grid_title = '예약 내역  상세'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat-key       = pv_key.
  gs_fcat-fieldname = pv_field.
  gs_fcat-ref_table = pv_table.
  gs_fcat-just      = pv_just.
  gs_fcat-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'SCHEDULEID'.
      gs_fcat-hotspot = abap_true.
    WHEN 'COUNTRYFROM'.
      gs_fcat-coltext = '출발국가(도시)'.
    WHEN 'COUNTRYTO'.
      gs_fcat-coltext = '도착국가(도시)'.
    WHEN 'DEPARTDATE'.
      gs_fcat-coltext = '출발날짜'.
    WHEN 'DEPARTTIME'.
      gs_fcat-coltext = '출발시간'.
    WHEN 'ARRIVEDATE'.
      gs_fcat-coltext = '도착날짜'.
    WHEN 'ARRIVETIME'.
      gs_fcat-coltext = '도착시간'.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_data .

  CLEAR : gt_disp_sch.

*-- Set Display Data
  LOOP AT gt_schedule ASSIGNING FIELD-SYMBOL(<fs>).

    CLEAR : gs_disp_sch.

*-- Insert Domain Value
    gs_disp_sch = VALUE #(
                            scheduleid  =   <fs>-scheduleid
                            countryfrom =   gt_dd07v[ KEY key domvalue_l = <fs>-countryfrom ]-ddtext
                            countryto   =   gt_dd07v[ KEY key domvalue_l = <fs>-countryto ]-ddtext
                            departdate  =   <fs>-departdate
                            departtime  =   <fs>-departtime
                            arrivedate  =   <fs>-arrivedate
                            arrivetime  =   <fs>-arrivetime
                         ).

    gt_disp_sch = VALUE #( BASE gt_disp_sch ( gs_disp_sch ) ).

  ENDLOOP.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_ROW_ID
*&      --> E_COLUMN_ID
*&---------------------------------------------------------------------*
FORM handle_hotspot_click  USING pv_row_id pv_column_id.

  CASE pv_column_id.

    WHEN 'SCHEDULEID'.

      PERFORM get_booking_data USING pv_row_id.

    WHEN 'BOOKINGID'.

      PERFORM call_ticket_alv.

  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_double_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_ROW
*&      --> E_COLUMN
*&---------------------------------------------------------------------*
FORM handle_double_click  USING pv_row pv_column.

**********************************************************************
* 오른쪽 상단 예약 목록에서 하단으로 상세 예약정보 출력
* ---나중에 결제여부 넣을 수 있으면 넣기 (PG 이후)
**********************************************************************
  DATA : lv_tabix   TYPE sy-tabix.

  CLEAR : gs_disp_book, gs_disp_detail, gt_disp_detail.
  READ TABLE gt_disp_book INTO gs_disp_book INDEX pv_row.

  " 예약 상세 데이터 구성
  gs_disp_detail = CORRESPONDING #( gs_disp_book ).
  gs_disp_detail-discountratetext = |{ gs_disp_detail-discountrate }%|.

  APPEND gs_disp_detail TO gt_disp_detail.

  CALL METHOD go_grid_detail->refresh_table_display.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog2
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog2  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_tfcat-key       = pv_key.
  gs_tfcat-fieldname = pv_field.
  gs_tfcat-ref_table = pv_table.
  gs_tfcat-just      = pv_just.
  gs_tfcat-emphasize = pv_emph.

  CASE pv_field.

    WHEN 'BOOKER'.
      gs_tfcat-coltext = '예약자 ID'.
    WHEN 'BOOKTYPE'.
      gs_tfcat-coltext = '예약유형'.
    WHEN 'COUNTRYFROM'.
      gs_tfcat-coltext = '출발국가(도시)'.
    WHEN 'COUNTRYTO'.
      gs_tfcat-coltext = '도착국가(도시)'.
    WHEN 'TOTALPRICE'.
      gs_tfcat-cfieldname = 'CURRENCY'.
    WHEN 'CURRENCY'.
      gs_tfcat-coltext = '통화'.
    WHEN 'STATUS_TEXT'.
      gs_tfcat-coltext = '예약상태'.
    WHEN 'TOTALTICKET'.
      gs_tfcat-coltext = '티켓수량(개)'.
  ENDCASE.

  APPEND gs_tfcat TO gt_tfcat.
  CLEAR gs_tfcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog3
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog3  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_bfcat-key       = pv_key.
  gs_bfcat-fieldname = pv_field.
  gs_bfcat-ref_table = pv_table.
  gs_bfcat-just      = pv_just.
  gs_bfcat-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'BOOKINGID'.
      gs_bfcat-hotspot = abap_true.
    WHEN 'BOOKER'.
      gs_bfcat-coltext = '예약자 ID'.
    WHEN 'ECONTICKET'.
      gs_bfcat-coltext = '이코노미 수량'.
    WHEN 'PRESTICKET'.
      gs_bfcat-coltext = '프레스티지 수량'.
    WHEN 'DISCOUNTRATETEXT'.
      gs_bfcat-coltext = '할인율'.
    WHEN 'DISCOUNTAMOUNT'.
      gs_bfcat-coltext = '할인금액'.
      gs_bfcat-cfieldname = 'CURRENCY'.
    WHEN 'TOTALPRICE'.
      gs_bfcat-cfieldname = 'CURRENCY'.
    WHEN 'UNIT_PRICE'.
      gs_bfcat-cfieldname = 'CURRENCY'.
    WHEN 'USED_MILEAGE'.
      gs_bfcat-cfieldname = 'CURRENCY'.
    WHEN 'TAX_AMOUNT'.
      gs_bfcat-cfieldname = 'CURRENCY'.
    WHEN 'CURRENCY'.
      gs_bfcat-coltext = '통화'.
    WHEN 'TOTALTICKET'.
      gs_bfcat-coltext = '티켓수량(개)'.
  ENDCASE.

  APPEND gs_bfcat TO gt_bfcat.
  CLEAR gs_bfcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_top_of_page
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_top_of_page .

  DATA : lr_dd_table  TYPE REF TO cl_dd_table_element,
         col_field    TYPE REF TO cl_dd_area,
         col_value    TYPE REF TO cl_dd_area,
         lv_low_text  TYPE sdydo_text_element,
         lv_high_text TYPE sdydo_text_element,
         lv_text      TYPE sdydo_text_element.

  CALL METHOD go_dyndoc_id->add_table
    EXPORTING
      no_of_columns = 2
      border        = '0'
    IMPORTING
      table         = lr_dd_table.

  CALL METHOD lr_dd_table->add_column
    IMPORTING
      column = col_field.

  CALL METHOD lr_dd_table->add_column
    IMPORTING
      column = col_value.
  CALL METHOD col_value->add_text
    EXPORTING
      text         = lv_text
      sap_emphasis = cl_dd_document=>heading  " heading 사용 → 더 굵고 크게
      sap_color    = cl_dd_document=>list_negative_inv.

  lv_text = '항공 스케줄별 예약 목록/상세정보 확인'.
  PERFORM add_row USING lr_dd_table col_field col_value '조회 용도 : ' lv_text.

  CLEAR lv_text.
  lv_low_text = |{ so_date-low+0(4) }년 { so_date-low+4(2) }월 { so_date-low+6(2) }일|.
  lv_high_text = |{ so_date-high+0(4) }년 { so_date-high+4(2) }월 { so_date-high+6(2) }일|.
  lv_text = |{ lv_low_text } ~ { lv_high_text }|.
  PERFORM add_row USING lr_dd_table col_field col_value '조회 기준일 :' lv_text.

  IF pa_schid IS NOT INITIAL.

    CLEAR lv_text.

    lv_text = pa_schid.
    PERFORM add_row USING lr_dd_table col_field col_value '조회 항공편 : ' lv_text.

  ENDIF.

  PERFORM set_top_of_page.
  PERFORM set_top_of_page.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form add_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LR_DD_TABLE
*&      --> COL_FIELD
*&      --> COL_VALUE
*&      --> P_
*&      --> LV_TEXT
*&---------------------------------------------------------------------*
FORM add_row USING pr_dd_table TYPE REF TO cl_dd_table_element
                   pv_col_field TYPE REF TO cl_dd_area
                   pv_col_value TYPE REF TO cl_dd_area
                   pv_field
                   pv_text.

  DATA : lv_text TYPE sdydo_text_element.

  lv_text = pv_field.
  CALL METHOD pv_col_field->add_text
    EXPORTING
      text         = lv_text
      sap_emphasis = cl_dd_document=>strong
      sap_color    = cl_dd_document=>list_heading_inv.

  CALL METHOD pv_col_field->add_gap EXPORTING width = 3.

  lv_text = pv_text.
  CALL METHOD pv_col_value->add_text
    EXPORTING
      text         = lv_text
      sap_emphasis = cl_dd_document=>heading
      sap_color    = cl_dd_document=>list_negative_inv.

  CALL METHOD pv_col_value->add_gap EXPORTING width = 3.

  CALL METHOD pr_dd_table->new_row.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_top_of_page
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_top_of_page .

  IF go_html_cntrl IS INITIAL.
    CREATE OBJECT go_html_cntrl
      EXPORTING
        parent = go_top_cont.
  ENDIF.

  CALL METHOD go_dyndoc_id->merge_document.
  go_dyndoc_id->html_control = go_html_cntrl.

  CALL METHOD go_dyndoc_id->display_document
    EXPORTING
      reuse_control = 'X'
      parent        = go_top_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_event
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_event .

  CALL METHOD go_dyndoc_id->initialize_document
    EXPORTING
      background_color = cl_dd_area=>col_textarea.

  CALL METHOD go_grid_sched->list_processing_events
    EXPORTING
      i_event_name = 'TOP_OF_PAGE'
      i_dyndoc_id  = go_dyndoc_id.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_OBJECT
*&      --> E_INTERACTIVE
*&---------------------------------------------------------------------*
FORM handle_toolbar  USING po_object TYPE REF TO cl_alv_event_toolbar_set
                           pv_interactive.

  CLEAR gs_button.
  gs_button-butn_type = '3'.
  APPEND gs_button TO po_object->mt_toolbar.

  CLEAR gs_button.
  gs_button-function  = 'ALL'.
  gs_button-quickinfo = '전체'.
  gs_button-text      = '전체'.
  APPEND gs_button TO po_object->mt_toolbar.

  CLEAR gs_button.
  gs_button-function  = 'CUST'.
  gs_button-quickinfo = '개인고객'.
  gs_button-text      = '개인고객'.
  APPEND gs_button TO po_object->mt_toolbar.

  CLEAR gs_button.
  gs_button-function  = 'BPID'.
  gs_button-quickinfo = '여행사고객'.
  gs_button-text      = '여행사고객'.
  APPEND gs_button TO po_object->mt_toolbar.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&---------------------------------------------------------------------*
FORM handle_user_command  USING pv_ucomm.

  DATA : lv_booktype TYPE string.

  CASE pv_ucomm.
    WHEN 'ALL'.
      lv_booktype = ''.
    WHEN 'CUST'.
      lv_booktype = '개인'.
    WHEN 'BPID'.
      lv_booktype = '단체'.
  ENDCASE.

  PERFORM show_book_dispay USING lv_booktype.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form show_book_dispay
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM show_book_dispay USING pv_booktype.

  DATA : lv_cnt TYPE i.

  CLEAR gt_disp_book.

  LOOP AT gt_book ASSIGNING FIELD-SYMBOL(<fs>).

    CLEAR : gs_disp_book.

    " 도메인 값 파싱
    gs_disp_book = VALUE #(
                          bookingid      = <fs>-bookingid
                          booker         = <fs>-booker
                          booktype       = <fs>-booktype
                          bookingdate    = <fs>-bookingdate
                          scheduleid     = <fs>-scheduleid
                          countryfrom    = gt_dd07v[ KEY key domvalue_l = <fs>-countryfrom ]-ddtext
                          countryto      = gt_dd07v[ KEY key domvalue_l = <fs>-countryto ]-ddtext
                          conditionid    = <fs>-conditionid
                          conditiontype  = <fs>-conditiontype
                          discountrate   = <fs>-discountrate
                          discountamount = <fs>-discountamount
                          used_mileage   = <fs>-used_mileage
                          totalticket    = <fs>-totalticket
                          totalprice     = <fs>-totalprice
                          currency       = <fs>-currency
                          status_text    = gt_dd07v[ KEY key domvalue_l = <fs>-book_status ]-ddtext
                         ).


    IF pv_booktype IS INITIAL. " [전체] 클릭 시

      gt_disp_book = VALUE #( BASE gt_disp_book ( gs_disp_book ) ).

    ELSE.                      " [개인] [여행사] 클릭 시
      IF <fs>-booktype EQ pv_booktype.

        gt_disp_book = VALUE #( BASE gt_disp_book ( gs_disp_book ) ).

      ENDIF.

    ENDIF.

  ENDLOOP.

  lv_cnt = lines( gt_disp_book ).
  MESSAGE s008 WITH lv_cnt.

*  LOOP AT gt_book ASSIGNING FIELD-SYMBOL(<fs>).
*
*    CLEAR : gs_disp_book.
*
*    IF <fs>-booktype EQ pv_booktype.
*
*      gs_disp_book = VALUE #(
*                            bookingid      = <fs>-bookingid
*                            booker         = <fs>-booker
*                            booktype       = <fs>-booktype
*                            bookingdate    = <fs>-bookingdate
*                            scheduleid     = <fs>-scheduleid
*                            countryfrom    = gt_dd07v[ KEY key domvalue_l = <fs>-countryfrom ]-ddtext
*                            countryto      = gt_dd07v[ KEY key domvalue_l = <fs>-countryto ]-ddtext
*                            conditionid    = <fs>-conditionid
*                            conditiontype  = <fs>-conditiontype
*                            discountrate   = <fs>-discountrate
*                            discountamount = <fs>-discountamount
*                            used_mileage   = <fs>-used_mileage
*                            totalticket    = <fs>-totalticket
*                            totalprice     = <fs>-totalprice
*                            currency       = <fs>-currency
*                            status_text    = gt_dd07v[ KEY key domvalue_l = <fs>-book_status ]-ddtext
*                           ).
*
*      gt_disp_book = VALUE #( BASE gt_disp_book ( gs_disp_book ) ).
*
*    ENDIF.
*
*  ENDLOOP.

  CALL METHOD go_grid_booking->refresh_table_display.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_booking_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_booking_data USING pv_row_id.

**********************************************************************
* 왼쪽 ALV에서 scheduleID 클릭해서 스케줄의 예약목록 조회
* 1) 탑승권 테이블, 가격조건 테이블, 마일리지 테이블 조인
* 2) 예약자 custid, bpid 한 컬럼에 표시하도록 파싱
* 3) 예약 타입 파싱
* 4) 총 결제 금액 계산
**********************************************************************
  DATA : lv_unitprice   TYPE wrbtr,
         lv_discount    TYPE i,
         lv_totalticket TYPE i,
         lv_totalprice  TYPE wrbtr.

  CLEAR : gs_schedule, gt_book.
  READ TABLE gt_schedule INTO gs_schedule INDEX pv_row_id.

  " 해당 스케줄의 예약건 조회
  SELECT a~bookingid a~bpid a~custid
         a~countryfrom a~countryto a~scheduleid a~flightid
         bookingdate totalticket totalprice a~currency a~book_status
         b~departdate
         c~class
         d~conditionid d~discountrate d~conditiontype
  INTO CORRESPONDING FIELDS OF TABLE gt_book
  FROM zc103sdt0011 AS a INNER JOIN zc103sdt0008 AS b
                         ON a~scheduleid EQ b~scheduleid
                         INNER JOIN zc103sdt0014 AS c
                         ON a~bookingid EQ c~bookingid
                         LEFT JOIN zc103sdt0003 AS d
                         ON a~conditionid EQ d~conditionid
  WHERE a~scheduleid EQ gs_schedule-scheduleid
  GROUP BY
         a~bookingid a~bpid a~custid
         a~countryfrom a~countryto a~scheduleid a~flightid
         bookingdate totalticket totalprice a~currency a~book_status
         b~departdate
         c~class
         d~conditionid d~discountrate d~conditiontype.

  IF gt_book IS INITIAL.
    MESSAGE s003 DISPLAY LIKE 'E'.
  ELSE.
    MESSAGE s008 WITH sy-dbcnt.
  ENDIF.

  " 가격계산 위한 노선 테이블 조회
  CLEAR : gt_price.
  SELECT country_fr country_to
         ppriceh ppricen ppricel epriceh epricen epricel
    INTO CORRESPONDING FIELDS OF TABLE gt_price
    FROM zc103fit0005
    WHERE country_fr EQ gs_schedule-countryfrom
      AND country_to EQ gs_schedule-countryto.

  " 결제 단계(판매오더)에서 사용된 마일리지, 판매 단가 등 조회
  SELECT bookingid unit_price tax_amount used_mileage
         a~currency total_amount  "conditionid
    INTO CORRESPONDING FIELDS OF TABLE gt_salesorder
    FROM zc103sdt0007 AS a INNER JOIN zc103sdt0006 AS b
                           ON a~orderid EQ b~orderid
    WHERE charged_item = 'A'. "여객 only

  SORT gt_salesorder BY bookingid ASCENDING.

**********************************************************************
  " display용 itab 구성
**********************************************************************
  CLEAR : gt_disp_book.
  LOOP AT gt_book ASSIGNING FIELD-SYMBOL(<fs>).

    CLEAR : gs_disp_book, gs_price, gs_salesorder.
    CLEAR : lv_unitprice, lv_discount, lv_totalticket, lv_totalprice.

    IF <fs>-bpid IS INITIAL.
      <fs>-booker = <fs>-custid.
      <fs>-booktype = '개인'.
    ELSEIF <fs>-custid IS INITIAL.
      <fs>-booker = <fs>-bpid.
      <fs>-booktype = '단체'.
    ENDIF.

    " used_mileage
    READ TABLE gt_salesorder INTO gs_salesorder WITH KEY bookingid = <fs>-bookingid
                                                BINARY SEARCH.
    <fs>-used_mileage = gs_salesorder-used_mileage.

    " unit price 계산
    READ TABLE gt_price INTO gs_price WITH KEY country_fr = <fs>-countryfrom
                                               country_to = <fs>-countryto.

    " unit price 불러오기 - 성수기/보통기/비수기
    CASE <fs>-departdate+4(2).
      WHEN '02' OR '03' OR '04' OR '11' . "비수기
        IF <fs>-class EQ 'E'.
          lv_unitprice = gs_price-epricel.
        ELSE.
          lv_unitprice = gs_price-ppricel.
        ENDIF.
      WHEN '05' OR '06'. " 보통기
        IF <fs>-class EQ 'E'.
          lv_unitprice = gs_price-epricen.
        ELSE.
          lv_unitprice = gs_price-ppricen.
        ENDIF.
      WHEN OTHERS. " 성수기
        IF <fs>-class EQ 'E'.
          lv_unitprice = gs_price-epriceh.
        ELSE.
          lv_unitprice = gs_price-ppriceh.
        ENDIF.
    ENDCASE.

    <fs>-unit_price = lv_unitprice.

    " discount amount 계산
    lv_totalticket = <fs>-totalticket.
    <fs>-discountamount = ( lv_unitprice * lv_totalticket ) * ( <fs>-discountrate / 100 ).

    " Tax 계산
    <fs>-tax_amount = ( lv_unitprice * lv_totalticket ) * ( 1 - <fs>-discountrate / 100 ) * ( 1 / 10 ) .

    " totalprice는 판매오더 결제되는 대로 booking 테이블 totalprice로 업데이트 됨 (밑줄 주석 가능)
    <fs>-totalprice = ( lv_unitprice * lv_totalticket ) - <fs>-discountamount + <fs>-tax_amount.

    " Insert parsed value (Domain value, booker, total price)
    gs_disp_book = VALUE #(
                            bookingid      = <fs>-bookingid
                            booker         = <fs>-booker
                            booktype       = <fs>-booktype
                            bookingdate    = <fs>-bookingdate
                            scheduleid     = <fs>-scheduleid
                            countryfrom    = gt_dd07v[ KEY key domvalue_l = <fs>-countryfrom ]-ddtext
                            countryto      = gt_dd07v[ KEY key domvalue_l = <fs>-countryto ]-ddtext
                            conditionid    = <fs>-conditionid
                            conditiontype  = <fs>-conditiontype
                            unit_price     = <fs>-unit_price
                            discountrate   = <fs>-discountrate
                            discountamount = <fs>-discountamount
                            used_mileage   = <fs>-used_mileage
                            tax_amount     = <fs>-tax_amount
                            totalticket    = <fs>-totalticket
                            totalprice     = <fs>-totalprice
                            currency       = <fs>-currency
                            status_text    = gt_dd07v[ KEY key domvalue_l = <fs>-book_status ]-ddtext
                         ).

    gt_disp_book = VALUE #( BASE gt_disp_book ( gs_disp_book ) ).


  ENDLOOP.

  CALL METHOD go_grid_booking->refresh_table_display.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form call_ticket_alv
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PV_ROW_ID
*&---------------------------------------------------------------------*
FORM call_ticket_alv .

  SET PARAMETER ID 'BOOKINGID' FIELD gs_disp_book-bookingid.
  SET PARAMETER ID 'SCHEDULEID' FIELD gs_disp_book-scheduleid.

  CALL TRANSACTION 'ZC103SD0004'.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
