*&---------------------------------------------------------------------*
*& Include          ZC103SDR0004F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form set_main_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_main_data .

*-- Set Mileage Data
  SELECT mileage_id, custid, occur_date, expire_date, event_type,
         bookingid, mile_amount, balance_after, mile_total, currency,
         erdat, ernam, erzet, aedat, aenam, aezet
    FROM zc103sdt0010
    INTO CORRESPONDING FIELDS OF TABLE @gt_body.

*-- Set Customer Mileage Data
  SELECT custid, name, email, phone, nation, mile_balance, currency,
         erdat, ernam, erzet, aedat, aenam, aezet
    FROM zc103sdt0002
    INTO CORRESPONDING FIELDS OF TABLE @gt_cusmile.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_mileage_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_mileage_data .

  LOOP AT gt_body INTO gs_body.

*-- Expired Mileage Date = 현재 날짜  -> '소멸'
    IF gs_body-expire_date LE sy-datum.
      gs_body-event_type = 'C'.
      gs_body-balance_after = 0.
    ENDIF.

*    IF gs_body-erdat IS INITIAL.
*      gs_body-erdat = sy-datum.
*      gs_body-ernam = sy-uname.
*      gs_body-erzet = sy-uzeit.
*    ELSE.
*      gs_body-aedat = sy-datum.
*      gs_body-aenam = sy-uname.
*      gs_body-aezet = sy-uzeit.
*    ENDIF.

*-- Update DB Table
    UPDATE zc103sdt0010 FROM gs_body.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_cus_mileage_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_cus_mileage_data .

  DATA : lv_total TYPE i.

*-- Set Customer Mileage Data
  LOOP AT gt_cusmile INTO gs_cusmile.

    CLEAR : lv_total.

    LOOP AT gt_body INTO gs_body WHERE custid = gs_cusmile-custid.

      lv_total += gs_body-balance_after.

    ENDLOOP.

*    IF gs_cusmile-erdat IS INITIAL.
*      gs_cusmile-erdat = sy-datum.
*      gs_cusmile-ernam = sy-uname.
*      gs_cusmile-erzet = sy-uzeit.
*    ELSE.
*      gs_cusmile-aedat = sy-datum.
*      gs_cusmile-aenam = sy-uname.
*      gs_cusmile-aezet = sy-uzeit.
*    ENDIF.

    gs_cusmile-mile_balance = lv_total.

    UPDATE zc103sdt0002 FROM gs_cusmile.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_mileage_status
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_mileage_status .

  DATA : BEGIN OF ls_miles,
           bookingid TYPE zc103sdt0010-bookingid,
         END OF ls_miles,
         lt_miles LIKE TABLE OF ls_miles.

*-- 판매오더 Line + Mileage 이력 연결
  SELECT DISTINCT s~bookingid
    FROM zc103sdt0007 AS s INNER JOIN zc103sdt0010 AS m
      ON s~bookingid = m~bookingid
    WHERE s~order_status = 'C'
    INTO CORRESPONDING FIELDS OF TABLE @lt_miles.

  LOOP AT lt_miles INTO ls_miles.

    IF ls_miles-bookingid IS NOT INITIAL.

      UPDATE zc103sdt0010
        SET event_type = 'C'
        WHERE bookingid = @ls_miles-bookingid.

    ENDIF.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_exchange_rate
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_exchange_rate .
**********************************************************************
*-- 한국 수출입 은행에서 API 받아오고, 매일 영업일 기준 11시 전후로 바뀌기 때문에
*-- 배치는 정기적으로 정오에 실행
**********************************************************************
  TYPES: BEGIN OF ty_exrate,
           cur_unit   TYPE string,
           deal_bas_r TYPE string,
         END OF ty_exrate.

  TYPES: tt_exrate TYPE STANDARD TABLE OF ty_exrate WITH EMPTY KEY.

  DATA: lo_http_client TYPE REF TO if_http_client,
        lv_url         TYPE string,
        lv_result      TYPE string,
        lt_exrate      TYPE tt_exrate,
        ls_exrate      TYPE ty_exrate,
        ls_save        TYPE zc103sdt0020,
        lv_rate_string TYPE string,
        lv_status_code TYPE i,
        lv_status_text TYPE string.

  DATA : lt_data TYPE TABLE OF zc103sdt0020.

  SELECT cur_unit, deal_bas_r, updated_on
    FROM zc103sdt0020
    INTO CORRESPONDING FIELDS OF TABLE @lt_data
    ORDER BY updated_on DESCENDING.

  READ TABLE lt_data INTO DATA(ls_data) INDEX 1.

  IF ls_data-updated_on NE sy-datum.

    CONSTANTS: gv_authkey TYPE string VALUE 'YMHsQPK3IB9pmb4TitCsPUpgxmB5Fx4r'.

    lv_url = |https://www.koreaexim.go.kr/site/program/financial/exchangeJSON?authkey={ gv_authkey }&searchdate={ sy-datum }&data=AP01|.

    " 1. HTTP Client 생성
    CALL METHOD cl_http_client=>create_by_url
      EXPORTING
        url    = lv_url
      IMPORTING
        client = lo_http_client.

*  IF sy-subrc NE 0 OR lo_http_client IS INITIAL.
*    WRITE :/ '클라이언트 생성 실패'.
*    RETURN.
*  ENDIF.

    " 2. 요청 보내기
    lo_http_client->send( ).
    lo_http_client->receive( ).

    " 3. HTTP 응답 상태 코드 확인
    CALL METHOD lo_http_client->response->get_status
      IMPORTING
        code   = lv_status_code
        reason = lv_status_text.

*  WRITE: / 'HTTP 상태 코드:', lv_status_code, lv_status_text.

*  IF lv_status_code NE 200.
*    WRITE: / '정상적인 응답이 아님. JSON 파싱 중단.'.
*    RETURN.
*  ENDIF.

    " 4. 응답 데이터 가져오기
    lo_http_client->response->get_cdata( RECEIVING data = lv_result ).
*  WRITE : / '응답 내용:', lv_result.

    " 5. JSON 파싱 (SAP 표준 클래스)
    CALL METHOD /ui2/cl_json=>deserialize
      EXPORTING
        json = lv_result
      CHANGING
        data = lt_exrate.

    " 6. 저장
    LOOP AT lt_exrate INTO ls_exrate.

      IF ls_exrate-cur_unit IS INITIAL OR ls_exrate-deal_bas_r IS INITIAL.
        CONTINUE.
      ENDIF.

      lv_rate_string = ls_exrate-deal_bas_r.
      REPLACE ALL OCCURRENCES OF ',' IN lv_rate_string WITH ''.

      IF lv_rate_string CN '0123456789.'.
        CONTINUE.
      ENDIF.

      CLEAR ls_save.
      ls_save-mandt        = sy-mandt.
      ls_save-cur_unit     = ls_exrate-cur_unit.
      ls_save-deal_bas_r   = CONV #( lv_rate_string ).
      ls_save-updated_on   = sy-datum.

      INSERT zc103sdt0020 FROM ls_save.

    ENDLOOP.

  ENDIF.

  COMMIT WORK.
*  MESSAGE '환율 갱신 완료됨' TYPE 'S'.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_srv_end_status
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_srv_end_line_status .

*-- For Sales Order Line Data
  DATA : BEGIN OF ls_line,
           orderid        TYPE zc103sdt0007-orderid,
           orderitemno    TYPE zc103sdt0007-orderitemno,
           bookingid      TYPE zc103sdt0007-bookingid,
           transportid    TYPE zc103sdt0007-transportid,
           order_status   TYPE zc103sdt0007-order_status,
           srv_end_status TYPE zc103sdt0007-srv_end_status,
         END OF ls_line,
         lt_line LIKE TABLE OF ls_line.

*-- For AMDP
  DATA : BEGIN OF ls_adate,
           bookingid  TYPE zc103sdt0007-bookingid,
           scheduleid TYPE zc103sdt0011-scheduleid,
           arrivedate TYPE zc103sdt0008-arrivedate,
         END OF ls_adate,
         lt_adate LIKE TABLE OF ls_adate.

*-- For Arrive Date + 1
  DATA : lv_arrivedate_plus_1 TYPE sy-datum.

*-- Get Sales Order Line Item
  SELECT orderid, orderitemno, bookingid, transportid, order_status, srv_end_status
    FROM zc103sdt0007
    WHERE order_status EQ 'N'
      AND srv_end_status EQ 'N'
    INTO CORRESPONDING FIELDS OF TABLE @lt_line.

*-- When all service end
  IF lt_line IS INITIAL.
    EXIT.
  ENDIF.

*-- Set srv_end_status -> 'Y'
  LOOP AT lt_line ASSIGNING FIELD-SYMBOL(<fs_line>).

*-- Separate Travel Agency, Cargo
    IF <fs_line>-bookingid IS NOT INITIAL.

*-- CALL amdp(Get Arrive Date)
      CLEAR : lt_adate.
      CALL METHOD zc1cds_amdp_get_adate=>get_travel_adate
        EXPORTING
          iv_bookingid    = <fs_line>-bookingid
        IMPORTING
          et_travel_adate = lt_adate.

      READ TABLE lt_adate INTO ls_adate INDEX 1.

*-- Check : Arrive Date - 1 > sy-datum
      lv_arrivedate_plus_1 = ls_adate-arrivedate + 1.
      IF sy-datum > lv_arrivedate_plus_1.
        <fs_line>-srv_end_status = 'Y'.
      ELSE.
        <fs_line>-srv_end_status = 'N'.
      ENDIF.

    ELSEIF <fs_line>-transportid IS NOT INITIAL.

*-- CALL amdp(Get Arrive Date)
      CLEAR : lt_adate.
      CALL METHOD zc1cds_amdp_get_adate=>get_cargo_adate
        EXPORTING
          iv_transportid = <fs_line>-transportid
        IMPORTING
          et_cargo_adate = lt_adate.

      READ TABLE lt_adate INTO ls_adate INDEX 1.

*-- Check : Arrive Date + 1 < sy-datum
      lv_arrivedate_plus_1 = ls_adate-arrivedate + 1.
      IF sy-datum > lv_arrivedate_plus_1.
        <fs_line>-srv_end_status = 'Y'.
      ELSE.
        <fs_line>-srv_end_status = 'N'.
      ENDIF.

    ENDIF.

  ENDLOOP.

*-- SET Line Item Table by lt_line
  LOOP AT lt_line INTO ls_line.
    UPDATE zc103sdt0007 SET srv_end_status = ls_line-srv_end_status
    WHERE orderid = ls_line-orderid
      AND orderitemno = ls_line-orderitemno.

    IF sy-subrc NE 0.
      ROLLBACK WORK.
      EXIT.
    ENDIF.
  ENDLOOP.

  IF sy-subrc EQ 0.
    COMMIT WORK.
    UNASSIGN <fs_line>.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_srv_end_head_status
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_srv_end_head_status .

  DATA : BEGIN OF ls_head,
           orderid        TYPE zc103sdt0006-orderid,
           order_status   TYPE zc103sdt0006-order_status,
           billing_status TYPE zc103sdt0006-billing_status,
           srv_end_status TYPE zc103sdt0006-srv_end_status,
         END OF ls_head,
         lt_head LIKE TABLE OF ls_head.

  DATA : BEGIN OF ls_line,
           orderid        TYPE zc103sdt0006-orderid,
           order_status   TYPE zc103sdt0006-order_status,
           srv_end_status TYPE zc103sdt0006-srv_end_status,
         END OF ls_line,
         lt_line LIKE TABLE OF ls_line.

  DATA : lv_tabix    TYPE sy-tabix,
         lv_srstatus VALUE 'Y'.

*-- Get head Data
  SELECT orderid, order_status, srv_end_status
    FROM zc103sdt0006
    WHERE order_status EQ 'N'
      AND srv_end_status EQ 'N'
      AND billing_status EQ 'N'
    INTO CORRESPONDING FIELDS OF TABLE @lt_head.

*-- Get line Data
  SELECT orderid, order_status, srv_end_status
    FROM zc103sdt0007
    WHERE order_status EQ 'N'
    INTO CORRESPONDING FIELDS OF TABLE @lt_line.

  SORT lt_line BY orderid ASCENDING.

*-- Set head Service End Status N -> Y
  LOOP AT lt_head ASSIGNING FIELD-SYMBOL(<fs_head>).

    lv_srstatus = 'Y'.

    LOOP AT lt_line INTO ls_line WHERE orderid = <fs_head>-orderid.
      IF ls_line-srv_end_status = 'N'.
        lv_srstatus = 'N'.
        EXIT.
      ENDIF.
    ENDLOOP.

    CASE lv_srstatus.
      WHEN 'Y'.
        <fs_head>-srv_end_status = 'Y'.
        UPDATE zc103sdt0006 SET srv_end_status = 'Y' WHERE orderid = <fs_head>-orderid.
      WHEN 'N'.
        <fs_head>-srv_end_status = 'N'.
        UPDATE zc103sdt0006 SET srv_end_status = 'N' WHERE orderid = <fs_head>-orderid.
    ENDCASE.

    IF sy-subrc EQ 0.
      COMMIT WORK.
    ELSE.
      ROLLBACK WORK.
    ENDIF.

  ENDLOOP.
ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
