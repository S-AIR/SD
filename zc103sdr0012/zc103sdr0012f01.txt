*&---------------------------------------------------------------------*
*& Include          ZC103SDR0012F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form set_credit_table_total_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_credit_table_total_value .

  DATA : lt_credit TYPE TABLE OF zc103sdt0004.

*-- Get Billing Data
  SELECT bpid, SUM( total_amount ) AS total_amount
    FROM zc103sdt0004
    WHERE billing_status EQ 'Y'
      AND mail_status EQ 'Y'
      AND doc_flag EQ 'Y'
      AND gjahr EQ @sy-datum+0(4)
    GROUP BY bpid
    ORDER BY bpid
    INTO CORRESPONDING FIELDS OF TABLE @lt_credit.

*-- Calculate Each bp's Total_amount
  LOOP AT lt_credit INTO DATA(ls_credit).
*-- Set Time Stamp
    ls_credit-aedat = sy-datum.
    ls_credit-aenam = sy-uname.
    ls_credit-aezet = sy-uzeit.

*-- Update Credit Table
    UPDATE zc103sdt0016 SET used_amt_year = ls_credit-total_amount
                        WHERE bpid EQ ls_credit-bpid.

    IF sy-subrc EQ 0.
      COMMIT WORK.
    ELSE.
      ROLLBACK WORK.
    ENDIF.
  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_credit_table_score
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_credit_table_score .

  DATA : lt_bill          TYPE TABLE OF zc103sdt0004,
         lt_payment_base  TYPE TABLE OF zc103sdt0001,
         lv_last_date     TYPE dats,
         lv_delay_day     TYPE i,
         lv_penalty_score TYPE i,
         lt_credit_base   TYPE TABLE OF zc103sdt0029,
         ls_credit_base   TYPE zc103sdt0029,
         lv_plus_date     TYPE n LENGTH 2,
         lv_base_date     TYPE i.

  DATA : BEGIN OF ls_payment,
           bpid        TYPE zc103sdt0001-bpid,
           paymentterm TYPE i,
         END OF ls_payment,
         lt_payment LIKE TABLE OF ls_payment.

*-- For Sum penalty_score
  TYPES : BEGIN OF ts_psum,
            bpid          TYPE zc103sdt0001-bpid,
            penalty_score TYPE i,
            delay_day     TYPE i,
          END OF ts_psum.

  DATA : ls_psumb TYPE ts_psum,
         lt_psumb LIKE TABLE OF ls_psumb.

*-- For Collect
  DATA : ls_psum TYPE ts_psum,
         lt_psum TYPE SORTED TABLE OF ts_psum
                 WITH UNIQUE KEY bpid.

  DATA : lt_credit TYPE TABLE OF zc103sdt0016,
         ls_credit TYPE zc103sdt0016.

*-- Get billing data
  SELECT billnum, bpid, billing_date, payment_date
    FROM zc103sdt0004
    WHERE gjahr EQ @sy-datum+0(4)
      AND custid EQ ''
      AND billing_status EQ 'Y'
      AND mail_status EQ 'Y'
      AND doc_flag EQ 'Y'
      AND credit_status EQ 'N'
    INTO CORRESPONDING FIELDS OF TABLE @lt_bill.

*-- Get each bp's paymentterm
  SELECT bpid, paymentterm
    FROM zc103sdt0001
    INTO CORRESPONDING FIELDS OF TABLE @lt_payment_base.

*-- Get credit Base data
  SELECT grade, score, amt_limit
    FROM zc103sdt0029
    INTO CORRESPONDING FIELDS OF TABLE @lt_credit_base.

*-- Get Credit Data
  SELECT bpid, bpname, bptype, grade, current_score, last_eval_date,
         acc_delay_days, blocked_flag, limit_amt_year, used_amt_year,
         eval_year, currency, dunning_date, erdat, ernam, erzet,
         aenam, aezet, aedat
    FROM zc103sdt0016
    INTO CORRESPONDING FIELDS OF TABLE @lt_credit.

  IF ( lt_bill IS INITIAL ).
    EXIT.
  ELSE.
*-- Change Value FIELD
    LOOP AT lt_payment_base INTO DATA(ls_payment_base).
      CLEAR : ls_payment.
      ls_payment-bpid = ls_payment_base-bpid.

      CASE ls_payment_base-paymentterm.
        WHEN 'T001'.
          ls_payment-paymentterm = 0.
        WHEN 'T030'.
          ls_payment-paymentterm = 30.
        WHEN 'T060'.
          ls_payment-paymentterm = 60.
      ENDCASE.

      APPEND ls_payment TO lt_payment.

    ENDLOOP.

*-- Calculate Credit Score
    SORT : lt_payment BY bpid ASCENDING.

    LOOP AT lt_bill INTO DATA(ls_bill).
      READ TABLE lt_payment INTO ls_payment WITH KEY bpid = ls_bill-bpid
                                            BINARY SEARCH.
      lv_plus_date = ls_payment-paymentterm.

      CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL'
        EXPORTING
          date      = ls_bill-billing_date
          days      = lv_plus_date
          months    = 0
          years     = 0
        IMPORTING
          calc_date = lv_last_date.

*-- Check Delay day
      lv_delay_day = ls_bill-payment_date - lv_last_date.

*-- Delay
*-- Calculate Penalty Score
      CLEAR : lv_penalty_score.
      IF ( lv_delay_day > 0 ).
        IF ( lv_delay_day > 0 AND lv_delay_day <= 3 ).
          lv_penalty_score = 0.
        ELSEIF ( lv_delay_day >= 4 AND lv_delay_day <= 7 ).
          lv_penalty_score = 5.
        ELSEIF ( lv_delay_day >= 8 AND lv_delay_day <= 15 ).
          lv_penalty_score = 10.
        ELSEIF ( lv_delay_day >= 16 ).
          lv_penalty_score = 20.
        ENDIF.
      ELSE.
        lv_penalty_score = 0.
        lv_delay_day = 0.
      ENDIF.

*-- Set sum ITab
      ls_psumb-bpid = ls_bill-bpid.
      ls_psumb-penalty_score = lv_penalty_score.
      ls_psumb-delay_day = lv_delay_day.

      APPEND ls_psumb TO lt_psumb.

*-- Update Credit_status(Billing Table)
      UPDATE zc103sdt0004 SET credit_status = 'Y'
                          WHERE billnum = ls_bill-billnum.
    ENDLOOP.

*-- Group by bpid
    LOOP AT lt_psumb INTO ls_psumb.
      COLLECT ls_psumb INTO lt_psum.
    ENDLOOP.

*-- Update credit ITab
    DATA : lv_tabix TYPE sy-tabix.
    SORT : lt_credit BY bpid ASCENDING.

    LOOP AT lt_psum INTO ls_psum.
      READ TABLE lt_credit INTO ls_credit WITH KEY bpid = ls_psum-bpid
                                          BINARY SEARCH.
      lv_tabix = sy-tabix.
      ls_credit-acc_delay_days += ls_psum-delay_day.
      ls_credit-current_score -= ls_psum-penalty_score.
      MODIFY lt_credit FROM ls_credit INDEX lv_tabix TRANSPORTING acc_delay_days current_score.
    ENDLOOP.

    LOOP AT lt_credit ASSIGNING FIELD-SYMBOL(<fs_cre>).
      IF ( <fs_cre>-current_score >= 90 ). " VVIP
        <fs_cre>-grade = 'VVIP'.
        <fs_cre>-blocked_flag = 'Y'.
      ELSEIF ( <fs_cre>-current_score >= 75 ) AND " VIP
             ( <fs_cre>-current_score < 90 ).
        <fs_cre>-grade = 'VIP'.
        <fs_cre>-blocked_flag = 'Y'.
      ELSEIF ( <fs_cre>-current_score >= 60 ) AND " GOLD
             ( <fs_cre>-current_score < 75 ).
        <fs_cre>-grade = 'GOLD'.
        <fs_cre>-blocked_flag = 'Y'.
      ELSEIF ( <fs_cre>-current_score >= 40 ) AND " SILVER
             ( <fs_cre>-current_score < 60 ).
        <fs_cre>-grade = 'SILVER'.
        <fs_cre>-blocked_flag = 'Y'.
      ELSEIF ( <fs_cre>-current_score < 40 ) AND
             ( <fs_cre>-current_score >= 0 ). " BRONZE
        <fs_cre>-grade = 'BRONZE'.
        <fs_cre>-blocked_flag = 'N'.
      ELSE.
        <fs_cre>-grade = 'BRONZE'.
        <fs_cre>-blocked_flag = 'N'.
        <fs_cre>-current_score = 0.
      ENDIF.

*-- Common Credit field
      <fs_cre>-last_eval_date = sy-datum.
      READ TABLE lt_credit_base INTO ls_credit_base WITH KEY grade = <fs_cre>-grade.
      <fs_cre>-limit_amt_year = ls_credit_base-amt_limit.
      <fs_cre>-eval_year = sy-datum+0(4).
      <fs_cre>-currency = 'KRW'.

*-- Set time Stamp
      IF ( <fs_cre>-erdat IS INITIAL ).
        <fs_cre>-erdat = sy-datum.
        <fs_cre>-erzet = sy-uzeit.
        <fs_cre>-ernam = sy-uname.
      ELSE.
        <fs_cre>-aedat = sy-datum.
        <fs_cre>-aezet = sy-uzeit.
        <fs_cre>-aenam = sy-uname.
      ENDIF.

    ENDLOOP.

    DATA(lv_count) = lines( lt_credit ).

*-- Update DB Table
    IF ( lv_count > 0 ) AND
       ( sy-subrc EQ 0 ).
      MODIFY zc103sdt0016 FROM TABLE lt_credit.
      COMMIT WORK.
    ELSE.
      ROLLBACK WORK.
    ENDIF.
  ENDIF.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
