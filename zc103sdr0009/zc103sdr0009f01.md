```abap
*&---------------------------------------------------------------------*
*& Include          ZC103SDR0009F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form set_init_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_init_value .

  pa_year = sy-datum(4).
  pa_quar = '1'.
  IF sy-datum+4(2) > 10.
    pa_quar = '4'.
  ELSEIF sy-datum+4(2) > 7.
    pa_quar = '3'.
  ELSEIF sy-datum+4(2) > 4.
    pa_quar = '2'.
  ELSE.
    pa_quar = '1'.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_quarter_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_quarter_value .

  " 분기 listbox 만들어줌
  DATA: lt_list TYPE vrm_values,
        ls_list TYPE vrm_value.

  CLEAR lt_list.

  ls_list-key  = '1'.
  ls_list-text = '1분기'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '2'.
  ls_list-text = '2분기'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '3'.
  ls_list-text = '3분기'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '4'.
  ls_list-text = '4분기'.
  APPEND ls_list TO lt_list.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id     = 'PA_QUAR'
      values = lt_list.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id     = 'GV_QUAR'
      values = lt_list.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_body_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_body_data .

*-- Set parameter
  gv_year = pa_year.
  gv_quar = pa_quar.
  gv_route = pa_rout.

*-- Get Domain Value
  SELECT
  FROM dd07v
  FIELDS domvalue_l, ddtext
  WHERE domname = 'ZC103D_FI_LAND'
  INTO CORRESPONDING FIELDS OF TABLE @gt_dd07v.

*-- Get Body data
  PERFORM get_perform_data. " 실적 데이터
  PERFORM set_body_data.    " 실적 데이터 파싱 & 전역변수 세팅
  PERFORM get_analyze_data. " 분석 데이터
  SORT gt_display BY departdate.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_perform_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_perform_data .

*-- Parse parameter
  CASE gv_quar.
    WHEN '1'.
      gv_date_from = |{ gv_year }0101|.
      gv_date_to   = |{ gv_year }0331|.
    WHEN '2'.
      gv_date_from = |{ gv_year }0401|.
      gv_date_to   = |{ gv_year }0630|.
    WHEN '3'.
      gv_date_from = |{ gv_year }0701|.
      gv_date_to   = |{ gv_year }0930|.
    WHEN '4'.
      gv_date_from = |{ gv_year }1001|.
      gv_date_to   = |{ gv_year }1231|.
    WHEN OTHERS.
      " 기본값: 전체 연도
      gv_date_from = |{ gv_year }0101|.
      gv_date_to   = |{ gv_year }1231|.
  ENDCASE.

  IF gv_date_from > sy-datum.
    MESSAGE s000 WITH '해당 기간(분기) 실적은 아직 조회할 수 없습니다.'  DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  gv_countryfrom = gv_route(2).
  gv_countryto = gv_route+2(2).

  SELECT a~scheduleid, performdate,
         p_seatsmax, p_seatsocc, e_seatsmax, e_seatsocc, occurate,
         flightid, countryfrom, countryto,
         departdate, arrivedate, flighttime
    FROM zc103sdt0009 AS a JOIN zc103sdt0008 AS b
                           ON a~scheduleid = b~scheduleid
    WHERE departdate BETWEEN @gv_date_from AND @gv_date_to
     AND ( countryfrom = @gv_countryfrom OR @gv_countryfrom IS INITIAL )
     AND ( countryto   = @gv_countryto OR @gv_countryto IS INITIAL )
    ORDER BY departdate ASCENDING
    INTO CORRESPONDING FIELDS OF TABLE @gt_body.

  IF  gt_body IS INITIAL.
    MESSAGE s003 DISPLAY LIKE 'E'.
    STOP.
  ELSE.
    MESSAGE s008 WITH sy-dbcnt.
  ENDIF.

  SORT gt_body BY departdate.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_body_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_body_data .

  CLEAR : gt_display, gv_total_cnt, gv_total_trip, gv_total_passenger, gv_avg_trip, gv_avg_passenger.

  LOOP AT gt_body ASSIGNING FIELD-SYMBOL(<fs>).

    CLEAR : gs_display.

    " 총 운항 시간
    gv_total_trip += <fs>-flighttime.

    " 총 탑승객 수
    gv_total_passenger += <fs>-p_seatsocc + <fs>-e_seatsocc.

    <fs>-total_acc = <fs>-p_seatsocc + <fs>-e_seatsocc.

    " 점유율 계산
    <fs>-p_occurate = ( <fs>-p_seatsocc / <fs>-p_seatsmax ) * 100.
    <fs>-e_occurate = ( <fs>-e_seatsocc / <fs>-e_seatsmax ) * 100.

    " Domain value
    gs_display = VALUE #(
                          scheduleid = <fs>-scheduleid
                          performdate = <fs>-performdate
                          p_seatsmax = <fs>-p_seatsmax
                          p_seatsocc = <fs>-p_seatsocc
                          e_seatsmax = <fs>-e_seatsmax
                          e_seatsocc = <fs>-e_seatsocc
                          p_occurate = <fs>-p_occurate
                          e_occurate = <fs>-e_occurate
                          occurate = <fs>-occurate
                          countryfr_text =   gt_dd07v[ KEY key domvalue_l = <fs>-countryfrom ]-ddtext
                          countryto_text =   gt_dd07v[ KEY key domvalue_l = <fs>-countryto ]-ddtext
                          departdate  =   <fs>-departdate
                          departtime  =   <fs>-departtime
                          arrivedate  =   <fs>-arrivedate
                          arrivetime  =   <fs>-arrivetime
                          flighttime = <fs>-flighttime
                          flightid  = <fs>-flightid
                          countryfrom = <fs>-countryfrom
                          countryto = <fs>-countryto
                          total_acc = <fs>-total_acc
                        ).

    gt_display = VALUE #( BASE gt_display ( gs_display ) ).

  ENDLOOP.

  " 전역변수 세팅
  gv_total_cnt = lines( gt_display ).
  gv_avg_trip = gv_total_trip / gv_total_cnt.
  gv_avg_passenger = gv_total_passenger / gv_total_cnt.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form before_100_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM before_100_screen .

  IF go_tab_cont1 IS NOT BOUND.

    PERFORM set_field_catalog.
    PERFORM set_layout.
    PERFORM create_object.

    PERFORM display_alv.
    PERFORM refresh_alv.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog.

  CLEAR : gt_fcat1, gs_fcat1, gt_fcat2, gs_fcat2,
          gt_fcat3, gs_fcat3, gt_fcat4, gs_fcat4.

  " 실적 field catalog
  PERFORM set_field_catalog1 USING : 'X' 'SCHEDULEID' 'ZC103SDT0009' 'C' ' ',
                                      ' ' 'DEPARTDATE' 'ZC103SDT0008' 'C' ' ',
                                      ' ' 'COUNTRYFR_TEXT' ' ' ' ' 'X',
                                      ' ' 'COUNTRYTO_TEXT' ' ' ' ' 'X',
                                      ' ' 'P_SEATSOCC' 'ZC103SDT0009' ' ' ' ',
                                      ' ' 'P_OCCURATE' ' ' ' ' ' ',
                                      ' ' 'E_SEATSOCC' 'ZC103SDT0009' ' ' ' ',
                                      ' ' 'E_OCCURATE' ' ' ' ' ' ',
                                      ' ' 'OCCURATE' 'ZC103SDT0009' ' ' 'X'.

  " 분석 1,2 field catalog
  PERFORM set_field_catalog2 USING : 'X' 'SCHEDULEID' 'ZC103SDT0009' 'C' ' ',
                                      ' ' 'DEPARTDATE' 'ZC103SDT0008' 'C' ' ',
                                      ' ' 'COUNTRYFR_TEXT' ' ' ' ' 'X',
                                      ' ' 'COUNTRYTO_TEXT' ' ' ' ' 'X',
                                      ' ' 'TOTAL_ACC' ' ' ' ' ' ' ,
                                      ' ' 'OCCURATE' 'ZC103SDT0009' ' ' 'X'.

  " 분석 3 field catalog
  PERFORM set_field_catalog3 USING : 'X' 'FLIGHTID' 'ZC103SDT0008' 'C' ' ',
                                      ' ' 'TRIP_CNT' ' ' ' ' ' ',
                                      ' ' 'AVERAGE_OCC' ' ' ' ' 'X'.

  " 분석 4 field catalog
  PERFORM set_field_catalog4 USING : 'X' 'ROUTE' ' ' ' ' ' ',
                                      ' ' 'TRIP_CNT' ' ' ' ' ' ',
                                      ' ' 'AVERAGE_OCC' ' ' ' ' 'X'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

*-- Set layout
  gs_layout-zebra      = abap_true.
  gs_layout-cwidth_opt = 'A'.
  gs_layout-sel_mode   = 'D'.
*  gs_layout-grid_title = '실적'.

  gs_layout1 = CORRESPONDING #( gs_layout ).
  gs_layout1-grid_title = '점유율 상위 10개 항공편'.

  gs_layout2 = CORRESPONDING #( gs_layout ).
  gs_layout2-grid_title = '점유율 하위 10개 항공편'.

  gs_layout3 = CORRESPONDING #( gs_layout ).
  gs_layout3-grid_title = '항공기별 운항 집계'.

  gs_layout4 = CORRESPONDING #( gs_layout ).
  gs_layout4-grid_title = '노선별 운항 집계'.

*-- Set Variant
  gs_variant-report = sy-repid.
  gs_variant-handle = 'ALV1'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

*-- 1. Main Container
  CREATE OBJECT go_tab_cont1
    EXPORTING
      container_name = 'MAIN_CONT1'.

  CREATE OBJECT go_alv_grid1
    EXPORTING
      i_parent = go_tab_cont1.

  CREATE OBJECT go_tab_cont2
    EXPORTING
      container_name = 'MAIN_CONT2'.

*-- 2. Split Container
  CREATE OBJECT go_split_main
    EXPORTING
      parent  = go_tab_cont2
      rows    = 2
      columns = 2.

*-- 2-1. Patch container
  CALL METHOD go_split_main->get_container
    EXPORTING
      row       = 1
      column    = 1
    RECEIVING
      container = go_split_cont1.

  CALL METHOD go_split_main->get_container
    EXPORTING
      row       = 1
      column    = 2
    RECEIVING
      container = go_split_cont2.

  CALL METHOD go_split_main->get_container
    EXPORTING
      row       = 2
      column    = 1
    RECEIVING
      container = go_split_cont3.

  CALL METHOD go_split_main->get_container
    EXPORTING
      row       = 2
      column    = 2
    RECEIVING
      container = go_split_cont4.

*-- 3. Patch ALV
  CREATE OBJECT go_split_grid1
    EXPORTING
      i_parent = go_split_cont1.

  CREATE OBJECT go_split_grid2
    EXPORTING
      i_parent = go_split_cont2.

  CREATE OBJECT go_split_grid3
    EXPORTING
      i_parent = go_split_cont3.

  CREATE OBJECT go_split_grid4
    EXPORTING
      i_parent = go_split_cont4.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_subscreen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_subscreen .

  " 서브스크린 설정
  CASE gc_tab-activetab.
    WHEN 'TAB1'.
      gv_subscreen = '0101'.
    WHEN 'TAB2'.
      gv_subscreen = '0102'.
    WHEN OTHERS.
      gc_tab-activetab = 'TAB1'.
      gv_subscreen = '0101'.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog1
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog1  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat1-key       = pv_key.
  gs_fcat1-fieldname = pv_field.
  gs_fcat1-ref_table = pv_table.
  gs_fcat1-just      = pv_just.
  gs_fcat1-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'COUNTRYFR_TEXT'.
      gs_fcat1-coltext = '출발국가'.
    WHEN 'COUNTRYTO_TEXT'.
      gs_fcat1-coltext = '도착국가'.
    WHEN 'P_SEATSOCC'.
      gs_fcat1-coltext = '프레스티지 점유 좌석'.
    WHEN 'E_SEATSOCC'.
      gs_fcat1-coltext = '이코노미 점유 좌석'.
    WHEN 'P_OCCURATE'.
      gs_fcat1-coltext = '프레스티지 점유율(%)'.
    WHEN 'E_OCCURATE'.
      gs_fcat1-coltext = '이코노미 점유율(%)'.
    WHEN 'OCCURATE'.
      gs_fcat1-coltext = '총 점유율(%)'.
  ENDCASE.

  APPEND gs_fcat1 TO gt_fcat1.
  CLEAR gs_fcat1.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog2
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog2  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat2-key       = pv_key.
  gs_fcat2-fieldname = pv_field.
  gs_fcat2-ref_table = pv_table.
  gs_fcat2-just      = pv_just.
  gs_fcat2-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'COUNTRYFR_TEXT'.
      gs_fcat2-coltext = '출발국가'.
    WHEN 'COUNTRYTO_TEXT'.
      gs_fcat2-coltext = '도착국가'.
    WHEN 'TOTAL_ACC'.
      gs_fcat2-coltext = '총 점유 좌석수(석)'.
    WHEN 'OCCURATE'.
      gs_fcat2-coltext = '총 점유율(%)'.
  ENDCASE.

  APPEND gs_fcat2 TO gt_fcat2.
  CLEAR gs_fcat2.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_alv
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_alv .

  CALL METHOD go_alv_grid1->set_table_for_first_display
    EXPORTING
      is_variant      = gs_variant
      i_save          = 'A'
      i_default       = 'X'
      is_layout       = gs_layout
    CHANGING
      it_outtab       = gt_display
      it_fieldcatalog = gt_fcat1.

  gs_variant-handle = 'SPLIT1'.
  CALL METHOD go_split_grid1->set_table_for_first_display
    EXPORTING
      is_variant      = gs_variant
      i_save          = 'A'
      i_default       = 'X'
      is_layout       = gs_layout1
    CHANGING
      it_outtab       = gt_display1
      it_fieldcatalog = gt_fcat2.

  gs_variant-handle = 'SPLIT2'.
  CALL METHOD go_split_grid2->set_table_for_first_display
    EXPORTING
      is_variant      = gs_variant
      i_save          = 'A'
      i_default       = 'X'
      is_layout       = gs_layout2
    CHANGING
      it_outtab       = gt_display2
      it_fieldcatalog = gt_fcat2.

  gs_variant-handle = 'SPLIT3'.
  CALL METHOD go_split_grid3->set_table_for_first_display
    EXPORTING
      is_variant      = gs_variant
      i_save          = 'A'
      i_default       = 'X'
      is_layout       = gs_layout3
    CHANGING
      it_outtab       = gt_display3
      it_fieldcatalog = gt_fcat3.

  gs_variant-handle = 'SPLIT4'.
  CALL METHOD go_split_grid4->set_table_for_first_display
    EXPORTING
      is_variant      = gs_variant
      i_save          = 'A'
      i_default       = 'X'
      is_layout       = gs_layout4
    CHANGING
      it_outtab       = gt_display4
      it_fieldcatalog = gt_fcat4.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_alv
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_alv .

  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable-row = abap_true.
  ls_stable-col = abap_true.

  IF go_alv_grid1 IS BOUND.
    CALL METHOD go_alv_grid1->refresh_table_display
      EXPORTING
        is_stable = ls_stable.
  ENDIF.

  IF go_split_grid1 IS BOUND.
    CALL METHOD go_split_grid1->refresh_table_display
      EXPORTING
        is_stable = ls_stable.
  ENDIF.

  IF go_split_grid2 IS BOUND.
    CALL METHOD go_split_grid2->refresh_table_display
      EXPORTING
        is_stable = ls_stable.
  ENDIF.

  IF go_split_grid3 IS BOUND.
    CALL METHOD go_split_grid3->refresh_table_display
      EXPORTING
        is_stable = ls_stable.
  ENDIF.

  IF go_split_grid4 IS BOUND.
    CALL METHOD go_split_grid4->refresh_table_display
      EXPORTING
        is_stable = ls_stable.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_data .

  pa_quar = ''.
  pa_rout = ''.
  gv_year = ''.
  gv_quar = ''.
  gv_route = ''.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_route_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_route_value .

  " 분기 listbox 만들어줌
  DATA: lt_list TYPE vrm_values,
        ls_list TYPE vrm_value.

  CLEAR lt_list.

  ls_list-key  = '0001'.
  ls_list-text = '대한민국(인천) ▶ 대한민국(부산/김해)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0002'.
  ls_list-text = '대한민국(인천) ▶ 오스트레일리아(시드니)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0003'.
  ls_list-text = '대한민국(인천) ▶ 베트남(하노이)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0004'.
  ls_list-text = '대한민국(인천) ▶ 아랍에미리트(두바이)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0005'.
  ls_list-text = '대한민국(인천) ▶ 독일(프랑크푸르트)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0006'.
  ls_list-text = '대한민국(인천) ▶ 미국(LA)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0100'.
  ls_list-text = '대한민국(부산/김해) ▶ 대한민국(인천)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0200'.
  ls_list-text = '오스트레일리아(시드니) ▶ 대한민국(인천)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0300'.
  ls_list-text = '베트남(하노이) ▶ 대한민국(인천)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0400'.
  ls_list-text = '아랍에미리트(두바이) ▶ 대한민국(인천)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0500'.
  ls_list-text = '독일(프랑크푸르트) ▶ 대한민국(인천)'.
  APPEND ls_list TO lt_list.

  ls_list-key  = '0600'.
  ls_list-text = '미국(LA) ▶ 대한민국(인천)'.
  APPEND ls_list TO lt_list.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id     = 'PA_ROUT'
      values = lt_list.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id     = 'GV_ROUTE'
      values = lt_list.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_analyze_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_analyze_data .

  CLEAR : gt_display1, gt_display2, gt_display3, gt_display4.

**********************************************************************
* 정렬 및 상위 10% 필터링
**********************************************************************
  SORT gt_display BY occurate DESCENDING.

  CLEAR : gs_display.

  LOOP AT gt_display INTO gs_display FROM 1 TO 10.
    APPEND gs_display TO gt_display1.
  ENDLOOP.

**********************************************************************
* 정렬 및 하위 10% 필터링
**********************************************************************
  SORT gt_display BY occurate ASCENDING.

  CLEAR : gs_display.

  LOOP AT gt_display INTO gs_display FROM 1 TO 10.
    APPEND gs_display TO gt_display2.
  ENDLOOP.

**********************************************************************
* 항공기별 운항 현황
**********************************************************************
  LOOP AT gt_display INTO gs_display.

    READ TABLE gt_display3 INTO gs_display3 WITH KEY flightid = gs_display-flightid.
    IF sy-subrc = 0.
      gs_display3-trip_cnt += 1.
      gs_display3-total_occ  += gs_display-occurate.
      MODIFY gt_display3 FROM gs_display3 INDEX sy-tabix.
    ELSE.
      gs_display3-flightid = gs_display-flightid.
      gs_display3-trip_cnt = 1.
      gs_display3-total_occ  = gs_display-occurate.
      APPEND gs_display3 TO gt_display3.
    ENDIF.

  ENDLOOP.

  LOOP AT gt_display3 INTO gs_display3.
    gs_display3-average_occ = gs_display3-total_occ / gs_display3-trip_cnt.
    MODIFY gt_display3 FROM gs_display3 INDEX sy-tabix.

  ENDLOOP.

  SORT gt_display3 BY trip_cnt DESCENDING.

**********************************************************************
* 노선별 운항 현황
**********************************************************************
  LOOP AT gt_display INTO gs_display.

    READ TABLE gt_display4 INTO gs_display4 WITH KEY countryfrom = gs_display-countryfrom
                                                     countryto   = gs_display-countryto.

    IF sy-subrc = 0.
      gs_display4-route = gs_display-countryfr_text && '▶' && gs_display-countryto_text.
      gs_display4-trip_cnt += 1.
      gs_display4-total_occ  += gs_display-occurate.
      MODIFY gt_display4 FROM gs_display4 INDEX sy-tabix.
    ELSE.
      gs_display4-countryfrom = gs_display-countryfrom.
      gs_display4-countryto   = gs_display-countryto.
      gs_display4-route = gs_display-countryfr_text && '▶' && gs_display-countryto_text.
      gs_display4-trip_cnt    = 1.
      gs_display4-total_occ   = gs_display-occurate.
      APPEND gs_display4 TO gt_display4.
    ENDIF.
  ENDLOOP.

  " 평균 계산
  LOOP AT gt_display4 INTO gs_display4.
    gs_display4-average_occ = gs_display4-total_occ / gs_display4-trip_cnt.
    MODIFY gt_display4 FROM gs_display4 INDEX sy-tabix.
  ENDLOOP.

  SORT gt_display4 BY trip_cnt DESCENDING.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog3
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog3  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat3-key       = pv_key.
  gs_fcat3-fieldname = pv_field.
  gs_fcat3-ref_table = pv_table.
  gs_fcat3-just      = pv_just.
  gs_fcat3-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'TRIP_CNT'.
      gs_fcat3-coltext = '총 운항 수(회)'.
    WHEN 'AVERAGE_OCC'.
      gs_fcat3-coltext = '평균 점유율(%)'.
  ENDCASE.

  APPEND gs_fcat3 TO gt_fcat3.
  CLEAR gs_fcat3.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog4
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog4  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat4-key       = pv_key.
  gs_fcat4-fieldname = pv_field.
  gs_fcat4-ref_table = pv_table.
  gs_fcat4-just      = pv_just.
  gs_fcat4-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'ROUTE'.
      gs_fcat4-coltext = '노선'.
    WHEN 'TRIP_CNT'.
      gs_fcat4-coltext = '총 운항 수(회)'.
    WHEN 'AVERAGE_OCC'.
      gs_fcat4-coltext = '평균 점유율(%)'.
  ENDCASE.

  APPEND gs_fcat4 TO gt_fcat4.
  CLEAR gs_fcat4.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form call_chart
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM call_chart .

  DATA: lv_url TYPE string.

  lv_url = 'http://bgissap1.bgissap.co.kr:8000/sap/bc/ui5_ui5/sap/zc103sdgw01/index.html?sap-client=100'.

  CALL METHOD cl_gui_frontend_services=>execute
    EXPORTING
      application = lv_url
    EXCEPTIONS
      OTHERS      = 1.

  IF sy-subrc <> 0.
    MESSAGE '웹 브라우저 실행 실패' TYPE 'E'.
  ENDIF.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758

